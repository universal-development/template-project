apply plugin: 'jetty'
apply plugin: 'war'


def springVersion = "4.0.6.RELEASE"
def springSecurityVersion = "3.2.5.RELEASE"
def unidevPlatformVersion = "1.2.0"

dependencies {

    compile project(":core")

    compile "org.springframework:spring-webmvc:$springVersion"
    compile "org.springframework:spring-context-support:$springVersion"

    compile "org.springframework.security:spring-security-web:$springSecurityVersion"
    compile "org.springframework.security:spring-security-config:$springSecurityVersion"

    compile "com.unidev.platform:platform-common-j2ee:$unidevPlatformVersion"

    compile 'org.jminix:jminix:1.2.0'

}

repositories {
    maven { url 'http://maven.restlet.org' }
}

jettyRun {
    httpPort = 8080
    contextPath = '/'
    daemon = false
}

jettyRunWar {
    httpPort = 8080
    contextPath = '/'
    daemon = false
}

[jettyRun, jettyRunWar, jettyStop]*.stopPort = 8081
[jettyRun, jettyRunWar, jettyStop]*.stopKey = 'stopKey'

import org.apache.tools.ant.filters.*
war {
    eachFile {
        if (it.name == 'build.jsp') {
            it.filter(ReplaceTokens, tokens: [
                    'build_id' : System.getProperty("BUILD_ID") + "",
                    'git_commit' : System.getProperty("GIT_COMMIT") + "",
                    'build_number' : System.getProperty("BUILD_NUMBER") + "",
                    'version' : project.version,
                    'name' : project.name,
                    'group' : project.group,
            ])
        }
    }
}
